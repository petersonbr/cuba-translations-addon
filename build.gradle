
buildscript {
    ext.cubaVersion = '7.2.0'
    ext.bprocVersion = '1.1'
    ext.idpVersion = '0.3'
    ext.dashboardVersion = '3.2'
    repositories {
        maven {
            url 'https://dl.bintray.com/cuba-platform/main'
        }
        jcenter()
    }
    dependencies {
        classpath "com.haulmont.gradle:cuba-plugin:$cubaVersion"
        // Bintray upload plugin
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

def modulePrefix = 'cuba-translations-addon'

def globalModule = project(":${modulePrefix}-global")
def coreModule = project(":${modulePrefix}-core")
def webModule = project(":${modulePrefix}-web")

def servletApi = 'javax.servlet:javax.servlet-api:3.1.0'

apply(plugin: 'cuba')

cuba {
    artifact {
        group = 'br.com.petersonbr.translations'
        version = '7.2.0'
        isSnapshot = false
    }
}

dependencies {
    appComponent("com.haulmont.cuba:cuba-global:$cubaVersion")
}

def hsql = 'org.hsqldb:hsqldb:2.4.1'

configure([globalModule, coreModule, webModule]) {
    apply(plugin: 'java')
    apply(plugin: 'maven')
    apply(plugin: 'cuba')

    dependencies {
            testCompile('org.junit.jupiter:junit-jupiter-api:5.5.2')
            testCompile('org.junit.jupiter:junit-jupiter-engine:5.5.2')
            testCompile('org.junit.vintage:junit-vintage-engine:5.5.2')  
        }

    task sourceJar(type: Jar) {
        from file('src')
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    } 
    test {
        useJUnitPlatform()
    }
}


configure(globalModule) {
    dependencies {
        if (!JavaVersion.current().isJava8()) {
            runtime('javax.xml.bind:jaxb-api:2.3.1')
            runtime('org.glassfish.jaxb:jaxb-runtime:2.3.1')
        }
    }

    entitiesEnhancing {
        main {
            enabled = true
        }
    }

    jar {
        manifest {
            attributes('App-Component-Id': cuba.artifact.group)
            attributes('App-Component-Version': cuba.artifact.version + (cuba.artifact.isSnapshot ? '-SNAPSHOT' : ''))
        }
    }
}

configure(coreModule) {

    configurations {
        jdbc
        dbscripts
    }

    dependencies {
        compile(globalModule)
        compileOnly(servletApi)
        jdbc(hsql)
        testRuntime(hsql)
    }

    task cleanConf(description: 'Cleans up conf directory', type: Delete) {
        delete "$cuba.appHome/${modulePrefix}-core/conf"
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}-core"
        appJars(modulePrefix + '-global', modulePrefix + '-core')
    }

    task createDb(dependsOn: assembleDbScripts, description: 'Creates local database', type: CubaDbCreation) {
    }

    task updateDb(dependsOn: assembleDbScripts, description: 'Updates local database', type: CubaDbUpdate) {
    }
}

configure(webModule) {
    configurations {
        webcontent
    }

    dependencies {
        compileOnly(servletApi)
        compile(globalModule)
    }

    task webArchive(type: Zip) {
        from file("$buildDir/web")
        from file('web')
        classifier = 'web'
    }

    artifacts {
        archives webArchive
    }

    task deployConf(type: Copy) {
        from file('src')
        include "br/com/petersonbr/translations/**"
        into "$cuba.appHome/${modulePrefix}/conf"
    }

    task clearMessagesCache(type: CubaClearMessagesCache) {
        appName = "${modulePrefix}"
    }
    deployConf.dependsOn clearMessagesCache

    task cleanConf(description: 'Cleans up conf directory', type: Delete) {
        delete "$cuba.appHome/${modulePrefix}/conf"
    }

    task deploy(dependsOn: [assemble, cleanConf], type: CubaDeployment) {
        appName = "${modulePrefix}"
        appJars(modulePrefix + '-global', modulePrefix + '-web')
    }
}

task undeploy(type: Delete, dependsOn: ":${modulePrefix}-web:cleanConf") {
    delete("$cuba.tomcat.dir/shared")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}-core")
    delete("$cuba.tomcat.dir/webapps/${modulePrefix}")
}

task restart(dependsOn: ['stop', ":${modulePrefix}-core:deploy", ":${modulePrefix}-web:deploy"], description: 'Redeploys applications and restarts local Tomcat') {
    doLast {
        ant.waitfor(maxwait: 6, maxwaitunit: 'second', checkevery: 2, checkeveryunit: 'second') {
            not {
                socket(server: 'localhost', port: '8787')
            }
        }
    }
}
restart.finalizedBy start

task updateSubmodules(type:Exec) {
    group 'Build Setup'
    description 'Updates (and inits) git submodules'
    def command = "git submodule update --init"
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'cmd', '/c', "$command"
    } else {
        commandLine 'sh', '-c', "$command"
    }
}

task copyTranslations(type:Copy) {
    from '/translations/content'
    into '/modules'
    includeEmptyDirs = false
    exclude { FileTreeElement details ->
        // remove english and russian translations already included in cuba
        // ['en', 'ru'].contains(details.path) ||
        // remove english translations inside other language translations folders
        //details.file.name.equals('messages.properties') ||

        //(!details.relativePath.pathString.startsWith('en') && details.file.name.equals('messages.properties'))
        // remove version 7.1 translations
        //details.path.endsWith('7.1')
    }
    eachFile { FileCopyDetails fcd ->
        def splitArray = fcd.path.split('/')
        def lang = splitArray[0]
        def project = splitArray[1]
        def version = splitArray[2]
        def module = splitArray[4]
        def filename = splitArray[splitArray.length-1];

        // remove english and russian translations already included in cuba
        if (['en', 'ru'].contains(lang)) {
            println "Excluding '$fcd.path'"
            fcd.exclude();
        }

        // Don't copy english translations inside other languages folders
        if ("messages.properties".equals(filename) && !'en'.equals(lang)) {
            println "Excluding '$fcd.path'"
            fcd.exclude();
        }

        // Filter Bproc version
        else if ('bproc'.equals(project) && !version.startsWith("$bprocVersion")) {
            println "Excluding '$fcd.path'"
            fcd.exclude();
        }

        // Filter Idp version
        else if ('idp'.equals(project) && !version.startsWith("$idpVersion")) {
            println "Excluding '$fcd.path'"
            fcd.exclude();
        }

        // Filter dashboard version
        else if ('dashboard'.equals(project) && !version.startsWith("$dashboardVersion")) {
            println "Excluding '$fcd.path'"
            fcd.exclude();
        }

        // Filter platform version
        else if (['cuba', 'bpm', 'reports', 'charts', 'fts'].contains(project) && !"$cubaVersion".contains(version)) {
            fcd.exclude();
        }

        else {
            def oldPath = fcd.path

            // Correctly set translation path
            fcd.path = fcd.path.replace("$lang/$project/$version/modules/$module", "$module")
            println "Copying file '$filename' from '$oldPath' -> '$fcd.path'"
        }
    }
}
copyTranslations.dependsOn updateSubmodules

task install
install.dependsOn copyTranslations

subprojects {
    apply plugin: 'com.jfrog.bintray'

    bintrayUpload.dependsOn copyTranslations

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')

        configurations = ['archives']

        publish = true

        override = false

        // metadata
        pkg {
            repo = 'cuba-components'
            name = 'cuba-translations-addon'
            desc = 'CUBA addon with all available community translations'

            websiteUrl = 'https://github.com/petersonbr/cuba-translations-addon'
            issueTrackerUrl = 'https://github.com/petersonbr/cuba-translations-addon/issues'
            vcsUrl = 'https://github.com/petersonbr/cuba-translations-addon.git' // mandatory for Open Source projects

            licenses = ["Apache-2.0"]

            labels = ['cuba', 'cuba-platform', 'cuba-component', 'translation', 'localization']
            githubRepo = 'petersonbr/cuba-translations-addon'
            //githubReleaseNotesFile = 'CHANGELOG.md' // optional Github readme file

            version {
                name = "${cuba.artifact.version}"
                desc = ''
                released = new Date()
                vcsTag = "${cuba.artifact.version}"
            }
        }
    }
}
